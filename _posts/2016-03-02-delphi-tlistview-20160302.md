---
layout: post
title: Delphi的TListView控件的使用演示
categories: delphi之控件
tags: delphi TListView 控件
---

##简单说明

* 参考[万一的博客](http://www.cnblogs.com/del/category/171107.html)，并且亲自实现
* 我的Delphi版本是Delphi 6，里面的一些代码逻辑可能会和万一的博客中有差异，不过最终的功能都是一样的
* 本文一共展示了四个例程，逐步深入对TListView控件的理解
* 相关的控件类有：TListView、TListItem、TListColumn、TCheckBox、TRadioButton……
* 相关的TListView的属性有：ViewStyle、Columns、GridLines、Checkboxes、RowSelect、Width……
* 详细请逐个研究下面示例的代码

##

![image](../image/2016-03-02/)

点击[这里](../download/2016-03-02)下载示例程序的完整代码。

```
unit Unit1;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, ComCtrls, StdCtrls;

type
  TForm1 = class(TForm)
    btn1: TButton;
    btn2: TButton;
    lv1: TListView;
    procedure btn1Click(Sender: TObject);
    procedure btn2Click(Sender: TObject);
  private
    { Private declarations }
  public
    { Public declarations }
  end;

var
  Form1: TForm1;

implementation

{$R *.dfm}

procedure TForm1.btn1Click(Sender: TObject);
begin
  lv1.Items.Add.Caption:= 'Item' + IntToStr(lv1.Items.Count);
end;

procedure TForm1.btn2Click(Sender: TObject);
begin
  lv1.Clear;
end;

end.
```

##

![image](../image/2016-03-02/)

点击[这里](../download/2016-03-02)下载示例程序的完整代码。

```
unit Unit1;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, StdCtrls, ComCtrls;

type
  TForm1 = class(TForm)
    lv1: TListView;
    rb1: TRadioButton;
    rb2: TRadioButton;
    rb3: TRadioButton;
    btn1: TButton;
    btn2: TButton;
    procedure FormCreate(Sender: TObject);
    procedure btn1Click(Sender: TObject);
    procedure btn2Click(Sender: TObject);
    procedure rb1Click(Sender: TObject);
    procedure rb2Click(Sender: TObject);
    procedure rb3Click(Sender: TObject);
  private
    { Private declarations }
  public
    { Public declarations }
  end;

var
  Form1: TForm1;

implementation

{$R *.dfm}

procedure TForm1.FormCreate(Sender: TObject);
begin
  lv1.Align:= alLeft;
  btn1.Caption:= '添加';
  btn2.Caption:= '清空';

  rb1.Caption:= 'ViewStyle = vsIcon';
  rb2.Caption:= 'ViewStyle = vsList';
  rb3.Caption:= 'ViewStyle = vsReport';
  rb1.Checked:= True;
end;

procedure TForm1.btn1Click(Sender: TObject);
var
  item: TListItem;
begin
  item:= lv1.Items.Add;
  item.Caption:= 'Item' + IntToStr(lv1.Items.Count);
end;

procedure TForm1.btn2Click(Sender: TObject);
begin
  lv1.Clear;
end;

procedure TForm1.rb1Click(Sender: TObject);
begin
  lv1.ViewStyle:= vsIcon;   {这是默认值}
end;

procedure TForm1.rb2Click(Sender: TObject);
begin
  lv1.ViewStyle:= vsList;
end;

procedure TForm1.rb3Click(Sender: TObject);
begin
  lv1.ViewStyle:= vsReport;
  if lv1.Columns.Count = 0 then
  begin
    lv1.Columns.Add.Caption:= '标题';
  end;
end;

end.
```

##

![image](../image/2016-03-02/)

点击[这里](../download/2016-03-02)下载示例程序的完整代码。

```
unit Unit1;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, ComCtrls, StdCtrls;

type
  TForm1 = class(TForm)
    chk1: TCheckBox;
    chk2: TCheckBox;
    btn1: TButton;
    btn2: TButton;
    lv1: TListView;
    procedure FormCreate(Sender: TObject);
    procedure btn1Click(Sender: TObject);
    procedure btn2Click(Sender: TObject);
    procedure chk1Click(Sender: TObject);
    procedure chk2Click(Sender: TObject);
  private
    { Private declarations }
  public
    { Public declarations }
  end;

var
  Form1: TForm1;

implementation

{$R *.dfm}

procedure TForm1.FormCreate(Sender: TObject);
var
  i: Integer;
  column: TListColumn;
begin
  for i:= 0 to 5 do
  begin
    column:= lv1.Columns.Add;
    column.Caption:= Format('Col %d', [i]);
  end;

  lv1.Align:= alTop;
  lv1.ViewStyle:= vsReport;
  lv1.GridLines:= True;           {非默认}
  lv1.ShowColumnHeaders:= True;   {默认}
  chk1.Caption:= 'GridLines';
  chk2.Caption:= 'ShowColumnHeaders';
  chk1.Checked:= True;
  chk2.Checked:= True;
  btn1.Caption:= '添加';
  btn2.Caption:= '清空';
end;

procedure TForm1.btn1Click(Sender: TObject);
var
  item: TListItem;
  i: Integer;
begin
  item:= lv1.Items.Add;
  item.Caption:= 'Item ' + IntToStr(item.Index);

  for i:= 0 to lv1.Columns.Count - 1do
  begin
    item.SubItems.Add(Format('%d%d', [item.Index, i]));
  end;
end;

procedure TForm1.btn2Click(Sender: TObject);
begin
  lv1.Items.Clear;
end;

procedure TForm1.chk1Click(Sender: TObject);
begin
  lv1.GridLines:= chk1.Checked;
end;

procedure TForm1.chk2Click(Sender: TObject);
begin
  lv1.ShowColumnHeaders:= chk2.Checked;
end;

end.
```

##

![image](../image/2016-03-02/)

点击[这里](../download/2016-03-02)下载示例程序的完整代码。

```
unit Unit1;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, StdCtrls, ComCtrls;

type
  TForm1 = class(TForm)
    lv1: TListView;
    chk1: TCheckBox;
    btn1: TButton;
    procedure FormCreate(Sender: TObject);
    procedure lv1Change(Sender: TObject; Item: TListItem;
      Change: TItemChange);
    procedure btn1Click(Sender: TObject);
    procedure chk1Click(Sender: TObject);
  private
    { Private declarations }
  public
    { Public declarations }
  end;

var
  Form1: TForm1;

implementation

{$R *.dfm}

procedure TForm1.FormCreate(Sender: TObject);
const
  ps: array[0..7, 0..2] of string = (
    ('赵', '男', '88'),
    ('钱', '男', '77'),
    ('孙', '女', '66'),
    ('李', '男', '55'),
    ('周', '女', '44'),
    ('吴', '女', '33'),
    ('郑', '男', '22'),
    ('王', '男', '11'));
var
  i: Integer;
  Item: TListItem;
  Column: TListColumn;
begin
  lv1.Align:= alTop;
  lv1.ViewStyle:= vsReport;
  lv1.GridLines:= True;
  lv1.Checkboxes:= True;    {显示复选框}

  //添加列的属性
  Column:= lv1.Columns.Add;
  Column.Caption:= '姓名';
  Column.Width:= lv1.Width div 4;

  Column:= lv1.Columns.Add;
  Column.Caption:= '性别';
  Column.Width:= lv1.Width div 4;

  Column:= lv1.Columns.Add;
  Column.Caption:= '年龄';
  Column.Width:= lv1.Width div 4;

  //添加行
  for i:= Low(ps) to High(ps) do
  begin
    Item:= lv1.Items.Add;
    Item.Caption:= ps[i][0];
    Item.SubItems.Add(ps[i][1]);
    Item.SubItems.Add(ps[i][2]);
  end;

  chk1.Caption:= 'RowSelect';
  btn1.Caption:= '显示复选的项';
end;

procedure TForm1.lv1Change(Sender: TObject; Item: TListItem;
  Change: TItemChange);
begin
  if Item.SubItems.Count > 0 then
  begin
    Text := Format('%s: %s: %s', [Item.Caption, Item.SubItems[0],
      Item.SubItems[1]]);
  end;
end;

procedure TForm1.btn1Click(Sender: TObject);
var
  Itm: TListItem;
  str: string;
  i: Integer;
begin
  str := '';
  {注意，在万一的博客中的源码的那种语法在Delphi6中不适用
    所以暂时改成这样的语法方式，效果是一样的}
  for i:= 0 to lv1.Items.Count - 1 do
  begin
    Itm:= lv1.Items.Item[i];
    if Itm.Checked then
    begin
      str:= str + Itm.Caption + sLineBreak;
    end;
  end;

  ShowMessage(TrimRight(str));
end;

procedure TForm1.chk1Click(Sender: TObject);
begin
  {RowSelect属性为True表示可以选中当前行的一整行
    为False则只能选择当前行的caption那列}
  lv1.RowSelect:= chk1.Checked;
end;

end.
```
