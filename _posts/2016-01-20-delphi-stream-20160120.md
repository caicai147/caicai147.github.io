---
layout: post
title: 初步了解和学习Delphi的流
categories: delphi之流 delphi之文件操作
tags: delphi 流 文件流 内存流
---

##简介

Stream对象，又称流式对象，是TStream、THandleStream、TFileStream、TMemoryStream、TResourceStream和TBlobStream等的统称。它们分别代表了在各种媒介上存储数据的能力，它们将各种数据类型（包括对象和部件）在内存、外村和数据库字段中的管理操作抽象为对象方法，并且充分利用了面向对象技术的优点，应用程序可以相当容易地在各种Stream对象中拷贝数据。

TStream对象是能在各种媒介中存储二进制数据的对象的抽象对象。从TStream对象继承的对象用于在内存、Windows资源文件、磁盘文件和数据库字段等媒介中存储数据。

Stream中定义了两个属性：Size和Position。它们分别以`字节`为单位表示流的大小和当前的指针位置。TStream中定义的方法用于在各种流中读、写和互相拷贝二进制数据。因为所有的Stream对象都是从TStream中继承来的，所以在TStream中定义的域和方法都能被Stream对象调用和访问。此外，又由于面向对象的动态联编功能，TStream为各种流的应用提供了统一的接口，简化了流的使用；不同Stream对象是抽象了对不同存储媒介的数据上的操作，因此，TStream的方法为在不同媒介间的数据拷贝提供了最简介的手段。

##内存流

所谓的“流”，就是一段数据或是一块内存

在进行流操作时，我们不必关心流中的数据到底是什么；只需要知道流的大小和当前的指针的位置，所以流只有两个属性：Size、Position

对流的操作，只不过就是读取和写入，所以流最主要的方法就是Read和Write。

在很多控件的使用中，读取主要用LoadFromStream，写入主要用SaveToStream

举个例子（(建立新工程, 添加两个 Memo、两个 Button）：

```
unit Unit1;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, StdCtrls;

type
  TForm1 = class(TForm)
    mmo1: TMemo;
    mmo2: TMemo;
    btn1: TButton;
    btn2: TButton;
    procedure FormCreate(Sender: TObject);
    procedure btn2Click(Sender: TObject);
    procedure btn1Click(Sender: TObject);
    procedure FormDestroy(Sender: TObject);
  private
    { Private declarations }
  public
    { Public declarations }
  end;

var
  Form1: TForm1;

implementation

{$R *.dfm}

var
  aStream: TStream; {声明一个流对象}

procedure TForm1.FormCreate(Sender: TObject);
begin
  aStream:= TMemoryStream.Create;
  {TStream是抽象类，只能通过其子类实例化；这里用内存流生成实例}

  mmo1.Lines.Text:= 'ABCDEFGHIJKLMNOPQRSTUVWXYZ';
  {给mmo1一个初始值}
end;

procedure TForm1.btn1Click(Sender: TObject);
begin
  mmo1.Lines.SaveToStream(aStream);     {把mmo1中的内容写到流}

  ShowMessage(IntToStr(aStream.Size));   {26, 当前流的大小}
  ShowMessage(IntToStr(aStream.Position));  {26, 当前流的指针}
end;

procedure TForm1.btn2Click(Sender: TObject);
begin
  aStream.Position:= 4;     {调整流当前的指针位置}
  mmo2.Lines.LoadFromStream(aStream); {读出流中的内容到mmo2}

  {现在mmo2中的内容应该是：EFGHIJKLMNOPQRSTUVWXYZ
    如果 Position 是 0, Memo2 读出的内容会是: ABCDEFGHIJKLMNOPQRSTUVWXYZ
    如果 Position 等于 Size, 在这里如果是 26, Memo2 就读不出什么了
    }
end;

procedure TForm1.FormDestroy(Sender: TObject);
begin
  aStream.Free;
  {流释放时，所有内存当然也会同时释放}
end;

{这个代码的例子，当你多次点击btn1，那么其会一直往内存流中写'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
  所以试着多次点击btn1和btn2，看运行效果，再结合代码来简单理解流
  }

end.
```

##文件流

TStream 是一个抽象的基类, 不能直接生成对象. 在具体的应用中, 主要使用它的子孙类:

* TFileStream: 文件流
* TStringStream: 字符串流
* TMemoryStream: 内存流
* TResourceStream: 资源文件流

详细说明一下

* THandleStream: 是 TFileStream 的父类、TStream 的子类
* TCustomMemoryStream: 是 TMemoryStream 和 TResourceStream 的父类、TStream 的子类
* 与流相关的常用类还有: TReader、TWriter、TCompressionStream、TDecompressionStream

举个文件流的例子：

```
unit Unit1;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, StdCtrls;

type
  TForm1 = class(TForm)
    btn1: TButton;
    procedure btn1Click(Sender: TObject);
  private
    { Private declarations }
  public
    { Public declarations }
  end;

var
  Form1: TForm1;

implementation

{$R *.dfm}

procedure TForm1.btn1Click(Sender: TObject);
var
  getStream, setStream: TStream;  {声明流}
  getPath, setPath: string;
  copySize: Integer;
begin
  {获取当前的进程的exe所在的地址}
  getPath:= SysUtils.ExtractFilePath(Application.ExeName) + 'get.jpg';
  {获取拷贝文件的地址}
  setPath:= SysUtils.ExtractFilePath(Application.ExeName) + 'set.jpg';

  if not FileExists(getPath) then
  begin
    ShowMessage('找不到我们要测试的图片文件: ' + getPath);
    Exit;
  end;

  getStream:= TFileStream.Create(getPath, fmOpenRead or fmShareExclusive);
  setStream:= TFileStream.Create(setPath, fmCreate);
  {建立文件流需要两个参数: 参数 1 是路径, 参数 2 是打开模式}
  {使用fmCreate，如果原来存在该文件，则会覆盖原来的文件}

  getStream.Position:= 0;    {流指针移到开始, 复制时从这里开始}
  copySize:= setStream.CopyFrom(getStream, getStream.Size);  {Copy流}
  {CopyFrom 的参数 2 是要复制的内容大小; 如果为 0 , 不管指针在什么位置都会复制所有内容}
  {CopyFrom 返回实际拷贝的字节数}

  ShowMessage('拷贝了' + IntToStr(copySize) + '字节！');

  {这时硬盘上就有 set.jpg 文件, 与 get.jpg 一模一样}
  {其实就是复制文件, 不过这里我们是用文件流实现的}

  getStream.Free;
  setStream.Free;
end;

end.
```
